#!/usr/bin/env zsh

_operation() {
  echo "${words[2]}"
}

_number_of_args() {
  echo "$#words"
}

_worktree_add_operation_completions() {
    local options

    [[ "$(_number_of_args)" = "4" ]] && options=("new-branch: The name of the new branch you want to create")

    _describe "operations" options
}

_worktree-operations() {
    # git-fzf worktree add
    [[ "$(_number_of_args)" -ge "4" ]] && [[ "${words[3]}" = "add" ]] && _worktree_add_operation_completions && return

    local operations

    operations=(
        "list: List details of each working tree. Press ESC to exit or select a worktree to move into it"
        "prune: Prune working tree information"
        "add: Create new working tree"
        "remove: Remove a working tree"
        "clean: Remove all working trees that do not have a corresponding remote branch"
    )

    _describe "operations" operations
}

_git-fzf-operations() {
    local operations

    operations=(
        "worktree: Worktree operations"
        "status: Show paths that have differences between the index file and the current HEAD commit"
        "branch: Show both local and remote branches"
        "checkout: Switch branch"
        "diff: Show changes between commits, commit and working tree, etc"
        "stash: Show stash entities"
        "reflog: Show reflog entities"
        "log: Show commit logs"
        "upgrade: Upgrade zsh-git-fzf plugin"
    )

    _describe "operations" operations
}

_git-fzf() {
    case "$(_operation)" in
        worktree)
        _worktree-operations
        ;;
    esac

    [[ "$(_number_of_args)" = "2" ]] &&  _git-fzf-operations
}

compdef _git-fzf git-fzf
